//
// =BEGIN MIT LICENSE
// 
// The MIT License (MIT)
//
// Copyright (c) 2014 Andras Csizmadia
// http://www.vpmedia.hu
// 
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
// 
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
// 
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE.
// 
// =END MIT LICENSE
//

import org.apache.tools.ant.taskdefs.condition.Os

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://raw.githubusercontent.com/vpmedia/mvnrepository/master'
    }
    maven {
        url 'https://oss.sonatype.org/content/groups/public/'
    }
}

configurations {
    antClassPath
    compile
    test
}

dependencies {
    test group: 'org.apache', name: 'flexunit-tasks', version: '4.2.0-20140410', ext: 'jar'
    test group: 'org.apache', name: 'flexunit-as3', version: '4.2.0-20140410', ext: 'swc'
    test group: 'org.apache', name: 'flexunit-cilistener', version: '4.2.0-20140410', ext: 'swc'
    test group: 'org.apache', name: 'flexunit-uilistener', version: '4.2.0-20140410', ext: 'swc'
    test group: 'org.apache', name: 'hamcrest-as3', version: '1.1.3', ext: 'swc'
    test group: 'org.apache', name: 'flex-pmd-ant-task', version: '1.3', ext: 'jar'
    test group: 'org.apache', name: 'flex-pmd-files', version: '1.3', ext: 'jar'
    test group: 'org.apache', name: 'flex-pmd-core', version: '1.3', ext: 'jar'
    test group: 'org.apache', name: 'flex-pmd-ruleset', version: '1.3', ext: 'jar'
    test group: 'org.apache', name: 'flex-pmd-ruleset-api', version: '1.3', ext: 'jar'
    test group: 'org.apache', name: 'flex-pmd-cpd', version: '1.3', ext: 'jar'
    test group: 'org.apache', name: 'flex-pmd-cpd-ant-task', version: '1.3', ext: 'jar'
    test group: 'org.apache', name: 'flex-pmd-metrics', version: '1.3', ext: 'jar'
    test group: 'org.apache', name: 'flex-pmd-metrics-ant-task', version: '1.3', ext: 'jar'
    test group: 'org.apache', name: 'as3-parser', version: '1.3', ext: 'jar'
    test group: 'org.apache', name: 'as3-parser-api', version: '1.3', ext: 'jar'
    test group: 'org.apache', name: 'as3-plugin-utils', version: '1.3', ext: 'jar'
    test group: 'pmd', name: 'pmd', version: '4.2.5', ext: 'jar'
    test group: 'commons-lang', name: 'commons-lang', version: '2.4', ext: 'jar'
    test group: 'org.codehaus.plexus', name: 'plexus-utils', version: '3.0.17', ext: 'jar'
    antClassPath 'ant-contrib:ant-contrib:1.0b3', 'org.apache.ant:ant-junit:1.9.4'
    compile project(':android')
}

if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    project(":extension").afterEvaluate { 
        extension -> extension.compile.dependsOn ':windows:package_all'
    }
} else {
    project(":extension").afterEvaluate { 
        extension -> extension.compile.dependsOn ':ios:package_all'
    }
}

// add additional jars to the AntBuilder classpath for use within the imported build.
ClassLoader antClassLoader = org.apache.tools.ant.Project.class.classLoader
configurations.antClassPath.each { File f ->
  antClassLoader.addURL(f.toURI().toURL())
} 

//----------------------------------
//  SDK
//----------------------------------

def AIR_HOME = System.getenv('AIR_HOME')
def FLEX_HOME = System.getenv('FLEX_HOME')
ant.FLEX_HOME = "${FLEX_HOME}"

//----------------------------------
//  Core
//----------------------------------

// Get version from Jenkins CI
if("${System.env.JENKINS_BUILD_VERSION}" != "null") {
    version = "${System.env.JENKINS_BUILD_VERSION}"
}

//----------------------------------
//  Folders
//----------------------------------

def binDir = new File(projectDir, '/bin')
def buildDir = new File(projectDir, '/build')
def reportDir = new File(projectDir, '/build/reports')
def publishDir = new File(projectDir, '/publish')
def tempDir = new File(projectDir, '/temp')
def confDir = new File(projectDir, '/src/main/temp')
def resourcesDir = new File(projectDir, '/src/main/resources')

//----------------------------------
//  Tasks
//----------------------------------

task clean << {
    if (binDir.isDirectory()) {
        delete(binDir)
    }
    if (buildDir.isDirectory()) {
        delete(buildDir)
    }
    if (confDir.isDirectory()) {
        delete(confDir)
    }
    if (tempDir.isDirectory()) {
        delete(tempDir)
    }
    if (publishDir.isDirectory()) {
        delete(publishDir)
    }
}

task copyDeps(type: Copy) {
    from configurations.all
    into binDir
}

task compile_init << {
    confDir.mkdirs()
    ant.copy(todir: 'src/main/temp') {
        fileset(dir : 'src/main/templates', includes: '*.*' )
    }    
    ant.replace(dir: "src/main/temp/", token: '${viewport_width}', value: '800')
    ant.replace(dir: "src/main/temp/", token: '${viewport_height}', value: '600')
    ant.replace(dir: "src/main/temp/", token: '${viewport_scale}', value: '1')
    ant.replace(dir: "src/main/temp/", token: '${version}', value: "${version}")
    ant.replace(dir: "src/main/temp/", token: '${debug}', value: 'true')
    ant.replace(dir: "src/main/temp/", token: '${player_version}', value: '15.0')
    ant.replace(dir: "src/main/temp/", token: '${swf_version}', value: '26')
    ant.replace(dir: "src/main/temp/", token: '${assets}', value: '')
    ant.replace(dir: "src/main/temp/", token: '${vars}', value: '')
}

task compile(type: JavaExec) { 
    main = "com.adobe.flash.compiler.clients.COMPC"
    classpath = files("${AIR_HOME}/lib/compiler.jar")
    workingDir = "${projectDir}"
    def argsList = [
                '-directory=false',
                '-debug=true',
                '-include-sources=src/main/actionscript',
                '-output=build/Main.swc',
                '-load-config+=src/main/temp/.flexConfig.xml'
            ]
    def airConfig = "${AIR_HOME}" + '/frameworks/air-config.xml'
    def loadAirConfig = '-load-config+='+"${airConfig}";
    argsList.push(loadAirConfig)
    args = argsList
}
            
task test << { 
    if (reportDir.isDirectory()) {
        delete("${reportDir}")
    }
    reportDir.mkdirs()
    ant.taskdef(resource: 'flexUnitTasks.tasks', classpath: configurations.test.asPath) 
    ant.flexunit(workingDir:'bin',toDir:'build/reports',haltonfailure:'false',verbose:'true',player:'flash',headless:'false',localTrusted:'true') {
        source(dir: 'src/main/actionscript')
        testSource(dir: 'src/test/actionscript') {
           include(name: '**/*Test.as')
        }
        library(dir: 'bin')
    }
    ant.junitreport(todir: reportDir) {
        fileset(dir: reportDir, includes: 'TEST-*.xml')
        report(format:'frames', todir:'build/reports/html')
    }
}
               
task analyze << { 
    reportDir.mkdirs()
    ant.taskdef(name: 'flexPmd', classname: 'com.adobe.ac.pmd.ant.FlexPmdAntTask', classpath: configurations.test.asPath)
    ant.taskdef(name: 'flexCpd', classname: 'com.adobe.ac.cpd.ant.FlexCpdAntTask', classpath: configurations.test.asPath)
    ant.flexPmd(sourceDirectory: 'src/main/actionscript', outputDirectory: reportDir )
}

task package_init << {
    copy {
        from fileTree(resourcesDir).files
        into tempDir    
    }
    copy {
        def swcFile = file(buildDir.absolutePath + '/Main.swc')
        from zipTree(swcFile)
        into tempDir    
    }  
    copy {
        from '../android/build/intermediates/bundles/release/jni'
        into tempDir.absolutePath + '/libs'
    }    
    ant.copy(file: '../android/build/intermediates/bundles/release/classes.jar', tofile: tempDir.absolutePath + '/ClientExtension.jar') 
    ant.copy(file: '../windows/ClientExtension.dll', tofile: tempDir.absolutePath + '/ClientExtension.dll')
}

task package_ane(type: JavaExec) {
    main = 'com.adobe.air.ADT'
    classpath = files("${AIR_HOME}/lib/adt.jar")
    workingDir = tempDir
    def argsList = [
        '-package',
        '-keystore',
        '../../tester/src/main/certs/gradlefx.p12',
        '-storetype',
        'pkcs12',
        '-storepass',
        'gradlefx',
        '-tsa',
        'none',
        '-target',
        'ane',
        'Main.ane',
        'extension.xml',
        '-swc',
        '../build/Main.swc',
        '-platform',
        'Android-ARM',
        'library.swf',
        'ClientExtension.jar',
        'libs',
        '-platform',
        'Windows-x86',
        'library.swf',
        'ClientExtension.dll',
        '-platform',
        'default',
        'library.swf'
            ]
    args = argsList
}

task package_all << {
    publishDir.mkdirs()
    ant.move(file: 'temp/Main.ane', todir: publishDir)
}

compile_init.dependsOn copyDeps
compile.dependsOn compile_init
package_init.dependsOn compile
package_ane.dependsOn package_init
package_all.dependsOn package_ane

defaultTasks 'clean', 'package_all', 'analyze'
